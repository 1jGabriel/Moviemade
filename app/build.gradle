apply plugin: 'com.android.application'
apply plugin: 'realm-android'

repositories {
    mavenCentral()
}

def getProperty(String filename, String propName) {
    def propsFile = rootProject.file(filename)
    if (propsFile.exists()) {
        def props = new Properties()
        props.load(new FileInputStream(propsFile))
        if (props[propName] != null) {
            return props[propName]
        } else {
            print("No such property " + propName + " in file " + filename)
        }
    } else {
        print(filename + " does not exist!")
    }
}

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 46
        versionName "0.3.6"
        buildConfigField "String", "VERSION_DATE", '"02 Nov 2018"'
        project.ext.set("archivesBaseName", "moviemade-v" + versionName)
        buildConfigField "String", "TMDB_API_KEY", "\"${getProperty("local.properties", "tmdb_api_key")}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "market", "develop"

    productFlavors {
        releasing {
            flavorDimensions "market"
            minSdkVersion 21
            applicationId "org.michaelbel.moviemade"
        }

        developing {
            flavorDimensions "develop"
            minSdkVersion 21
            versionName "0.3.6-beta"
            applicationId "org.michaelbel.moviemade.beta"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    realm {
        syncEnabled = true
    }

    /*dataBinding {
        enabled true
    }*/
}

dependencies {
    // android support
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:cardview-v7:28.0.0'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:palette-v7:28.0.0'
    // rxjava
    implementation 'io.reactivex.rxjava2:rxjava:2.1.13'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    // retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    // realm
    implementation 'io.realm:android-adapters:2.1.1'
    // moxy
    implementation 'com.arello-mobile:moxy:1.5.5'
    annotationProcessor 'com.arello-mobile:moxy-compiler:1.5.5'
    // dagger
    implementation 'com.google.dagger:dagger:2.16'
    implementation 'com.google.dagger:dagger-android-support:2.15'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.14.1'
    annotationProcessor 'com.google.dagger:dagger-android-processor:2.16'
    compileOnly 'javax.annotation:jsr250-api:1.0'
    // butterknife
    implementation 'com.jakewharton:butterknife:9.0.0-rc1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:9.0.0-rc1'



    implementation 'com.alexvasilkov:events:1.0.0'
    implementation 'com.alexvasilkov:gesture-views:2.4.0'
    implementation 'com.alexvasilkov:android-commons:2.0.2'

    implementation 'com.squareup.picasso:picasso:2.71828'

    implementation 'org.michaelbel:material:0.3.6'
    implementation 'org.michaelbel:bottomsheet:1.2.3'
    
    implementation 'at.blogc:expandabletextview:1.0.5'
    implementation 'com.beloo.widget:ChipsLayoutManager:0.3.7'

    implementation 'com.github.bumptech.glide:glide:4.8.0'
}

apply plugin: 'com.google.gms.google-services'